class RaceGame
{
   field boolean isRunning;
   field int enemiesCount;
   field PlayerCar playerCar;
   field Array enemies;
   field Array enemySpawnYCoords;
   field Road road;           
   field Random random;          

   constructor RaceGame new()
   {
      var EnemyCar currentEnemy;
      var int i;

      //initialize player car
      let playerCar = PlayerCar.new(28, 96);

      let enemiesCount = 16;
      let enemies = Array.new(enemiesCount);
      let i = 0;
      while (i < enemiesCount)
      {
         let enemies[i] = EnemyCar.new(16, 16);
         let currentEnemy = enemies[i];
         do currentEnemy.die();

         let i = i + 1;
      }

      //initialize road
      let road = Road.new();

      let enemySpawnYCoords = Array.new(4);
      let enemySpawnYCoords[0] = 16;
      let enemySpawnYCoords[1] = 80;
      let enemySpawnYCoords[2] = 144;
      let enemySpawnYCoords[3] = 208;

      return this;
   }

   /** Runs the game: handles the user's inputs and moves the car accordingly */
   method void run()
   {
      var int randomSeed, tick, frame;
      var char key;  // the key currently pressed by the user

      let randomSeed = handleStartGame();
      let random = Random.new(randomSeed);

      let isRunning = true;
      let tick = 0;
      let frame = 0;

      while (isRunning)
      {
         let key = Keyboard.keyPressed();

         if (tick = 5000)
         {
            do drawFrame(key);
            
            let tick = 0;
            let frame = frame + 1;
         }

         if (frame = 10)
         {
            do trySpawnEnemy();

            let frame = 0;
         }

         let tick = tick + 1;
      }

     return;
   }

   method int handleStartGame()
   {
      var int randomSeed;
      
      while (~(Keyboard.keyPressed() = 128))  // enter key
      {
         let randomSeed = randomSeed + 1;
         if (randomSeed = 32767)
         {
            let randomSeed = 0;
         }
      }

      return randomSeed;
   }

   method void drawFrame(int key)
   {
      if (key = 140)  { let isRunning = false; } // esc key
      if (key = 131) { do playerCar.move(0, -15); }   // up arrow
      if (key = 133) { do playerCar.move(0, 15); }   // down arrow   

      do Screen.clearScreen();
      do road.moveMarking();
      do road.drawMarking();
      do playerCar.draw();
      do drawEnemies();

      return;
   }

   method void drawEnemies()
   {
      var EnemyCar currentEnemy;
      var int i;

      let i = 0;
      while (i < enemiesCount)
      {
         let currentEnemy = enemies[i];

         if (currentEnemy.isAlive())
         {
            do currentEnemy.move(10, 0);

            // не доехал ли до края
            if (~(currentEnemy.onScreen()))
            {
               do currentEnemy.die();
            }
            
            // не случился ли конфликт
            else
            {
               do tryFindConflict(currentEnemy.GetX(), currentEnemy.GetY(), playerCar.GetX(), playerCar.GetY());
            }
            
            do currentEnemy.draw();
         }

         let i = i + 1;
      }

      return;
   }

   method void trySpawnEnemy()
   {
      var EnemyCar newEnemy, currentEnemy;
      var int case, i;

      let case = random.next(10);
      if (case < 4)
      {
         let i = 0;
         let currentEnemy = enemies[i];
         while ((i < (enemiesCount-1)) & (currentEnemy.isAlive()))
         {
            let i = i + 1;
            let currentEnemy = enemies[i];
         }

         if (~(currentEnemy.isAlive()))
         {
            do currentEnemy.reborn(0, enemySpawnYCoords[case]);
         }
      }

      return;
   }

   method void tryFindConflict(int xEnemy, int yEnemy, int xPlayer, int yPlayer)
   {
      var int leftEnemyX, topLeftEnemyY, bottomLeftEnemyY;
      var int rightPlayerX, topRightPlayerY, bottomRightPlayerY;
      var int error;

      var boolean isConflictFromBelow, isConflictFromAbove;

      let leftEnemyX = xEnemy + 47;
      let topLeftEnemyY = yEnemy;
      let bottomLeftEnemyY = yEnemy + 31;

      let rightPlayerX = xPlayer;
      let topRightPlayerY = yPlayer;
      let bottomRightPlayerY = yPlayer + 31;

      let isConflictFromBelow = (~(topRightPlayerY > bottomLeftEnemyY)) & (~(topRightPlayerY < topLeftEnemyY)) & (~(rightPlayerX > leftEnemyX));
      let isConflictFromAbove = (~(bottomRightPlayerY > bottomLeftEnemyY)) & (~(bottomRightPlayerY < topLeftEnemyY)) & (~(rightPlayerX > leftEnemyX));

      if (isConflictFromBelow | isConflictFromAbove)
      {
         let error = Sys.error(0);
      }

      return;
   }
   
   method void dispose()
   {
      // do car.dispose();
      do Memory.deAlloc(this);
      return;
   }
}



